buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://dist.creeper.host/Sponge/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id 'com.github.gmazzo.buildconfig' version '3.0.1'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply from: 'https://raw.githubusercontent.com/LordGrimmauld/gitinfo-collect/master/build.gradle'


version = "mc${minecraft_version}_v${mod_version}"
group = "de.oshgnacknak.${modid}"
archivesBaseName = "${modid}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets {
    main.java.srcDir "src/main/java"
    main.resources.srcDir 'src/main/resources'
}

minecraft {
    // mappings channel: 'official', version: minecraft_version
    mappings channel: 'parchment', version: "2022.06.05-${minecraft_version}" // https://ldtteam.jfrog.io/ui/native/parchmentmc-public/org/parchmentmc/data/parchment-1.18.2/
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        all {
            lazyToken('minecraft_classpath') {
                configurations.shadow.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
            }
        }

        client {
            workingDirectory project.file('run')
            arg "-mixin.config=${modid}.mixins.json"
            jvmArgs "-Dmixin.env.disableRefMap=true"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main

            mods {
                create_ponder_wonder {
                    source sourceSets.main
                }
            }
        }
    }
}

compileJava {
    options.compilerArgs = ['-Xdiags:verbose']
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}

configurations {
    library
    implementation.extendsFrom library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

repositories {
    maven {
        // tterrag
        url "https://maven.tterrag.com"
    }
    mavenCentral()
    maven {
        url "https://www.dcm4che.org/maven2"
    }
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("com.jozufozu.flywheel:Flywheel-Forge:1.18-0.6.3.81")
    implementation fg.deobf("com.simibubi.create:Create:mc1.18.2_v0.5.0+130")

    library 'xuggle:xuggle-xuggler:5.4'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

compileJava.options.encoding = 'UTF-8'

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(["Specification-Title"     : "${modid}",
                    "Specification-Vendor"    : "Oshgnacknak",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "Oshgnacknak",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "MixinConfigs"            : "${modid}.mixins.json"])
    }
    from {
        configurations.library.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

jar.finalizedBy('reobfJar')

buildConfig {
    buildConfigField 'String', 'MODID', "\"${modid}\""
    buildConfigField 'String', 'VERSION', "\"${project.version}\""
    buildConfigField "String", "GITHASH", "\"${getGitInfo()}\""

    def file = new File("imgur.properties")
    if (!file.exists()) {
        buildConfigField "String", "IMGUR_CLIENTID", "null"
    } else {
        file.withReader {
            Properties props = new Properties()
            props.load(it)
            buildConfigField "String", "IMGUR_CLIENTID", "\"${props.client_id}\""
        }
    }

    packageName = project.group
}

